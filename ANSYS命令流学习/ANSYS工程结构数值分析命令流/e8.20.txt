!EX8.20 悬索直接迭代求解
finish
/clear
/filname,ex820
/prep7
l0=120
xh=20
area=7.016e-4
em=7.89e10
q0=65
qf=10000
h0=9000
err0=1/1000
enum=60
istran=1.0e-6
et,1,link10
r,1,area,istran
mp,ex,1,em
mp,prxy,1,0.3
mp,dens,1,q0/area
!在弦线位置创建模型，施加自重荷载
k,1
k,2,l0,-xh
l,1,2
lesize,all,,,enum
lmesh,all
d,node(0,0,0),all
d,node(l0,-xh,0),all
node1=nelem(enum/2,1)
node2=nelem(enum/2,2)
acel,,1.0
finish
!求解、更新、判别收敛条件
pass1=1
*dowhile,pass1
/solu
antype,0
nlgeom,on
sstif,on
nsubst,20
outres,all,all
solve
finish
/post1
set,last,last
*get,nfor,elem,enum/2,smisc,1
cosref=(nx(node2)-nx(node1))/distnd(node1,node2)
nfor=nfor*abs(cosref)
err1=abs(nfor-h0)/h0
finish
/prep7
*if,err1,lt,0.05,then
upgeom,0.1,last,last,ex820,rst
*else
upgeom,1,last,last,ex820,rst
*endif
finish
*if,err1,lt,err0,exit
*enddo
!获得初始状态的索长等
/post1
set,last,last
plesol,smisc,1
etable,epelt,lepel,1
s=0
ds=0
*do,i,1,enum
*get,eleng,elem,i,leng
*get,epel,elem,i,etab,epelt
s=s+eleng
ds=ds+eleng*epel
*enddo
s0=s-ds
!求解外荷载作用下的内力和变形
/solu
f,node1,fy,-qf
nlgeom,on
nsubst,20
outres,all,all
solve
finish

