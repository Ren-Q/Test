!EX8.21 悬索找形分析法求解
finish
/clear
/filname,ex821
/prep7
l0=120
area=7.016e-4
q0=65
qf=10000
h0=9000
xh=20.0
enum=60
istran=0.999
em=h0/(area*istran)*sqrt(l0*l0+xh*xh)/l0
et,1,link10
r,1,area,istran
mp,ex,1,em
mp,prxy,1,0.3
mp,dens,1,q0/area/(1-istran)
!在弦线位置创建模型，施加自重荷载
k,1
k,2,l0,-xh
l,1,2
lesize,all,,,enum
lmesh,all
d,node(0,0,0),all
d,node(l0,-xh,0),all
d,all,ux
acel,,1.0
finish

/solu
antype,0
nlgeom,on
sstif,on
nsubst,20
outres,all,all
solve
finish

/prep7
upgeom,1,last,last,ex821,rst
finish

!确定形状后，恢复真实材料特性等，求解初始状态
finish
/prep7
em=7.89e10
istran=1.0e-6
mp,ex,1,em
mp,dens,1,q0/area
r,1,area,istran
finish
/solu
ddele,all,all
d,node(0,0,0),all
d,node(l0,-xh,0),all
acel,,1.0
solve
finish

!如果该初始状态与所设置的H0不同，也可迭代几次
!求解外荷载作用下的内力和变形
/solu
f,node1,fy,-qf
nlgeom,on
nsubst,20
outres,all,all
solve
finish

