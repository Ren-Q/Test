!ex9.16a 梁上移动质量的瞬态分析-位移荷载+耦合法
finish
/clear
/config,nres,2000
/prep7
lb=10   !梁长度
ne=50   !梁划分的单元数
nn=ne+1 !节点数
nn1=nn+1
ne1=ne+1
p=10000  !荷载值
v=36*1e3/3600 !移动速度36km/h
deltl=lb/ne
deltt=deltl/v  !移动一个单元所需时间

em=2.1e11
area=0.18
im=0.0054
deng=7800
gra=9.8
f1=acos(-1)/2/lb/lb*sqrt(em*im/(area*deng))

et,1,beam3
et,2,mass21,,,4
mp,ex,1,em
mp,nuxy,1,0.2
mp,dens,1,deng
r,1,area,im,1.0
r,2,p/gra
*do,i,1,nn
n,i,(i-1)*deltl
*enddo
*do,i,1,ne
e,i,i+1
*enddo

nsel,none
n,nn1
type,2
real,2
e,nn1
nsel,all

d,1,ux,,,,,uy
d,nn,uy

d,nn1,all

/solu
antype,4
sstif,on
timint,off
time,1e-5
acel,,gra
nsubst,2
kbc,1
solve

timint,on
outres,all,all
deltim,deltt/10
kbc,1
autots,on

ddele,nn1,uy
*do,i,1,nn
cpdele,all
d,nn1,ux,(i-1)*deltl
cp,next,uy,i,nn1
time,i*deltt
solve
*enddo

cpdele,all
d,nn1,ux,lb+1
d,nn1,uy
time,lb/v+5/f1
solve
finish
/post26
nc=node(lb/2,0,0)
nsol,2,nc,u,y
plvar,2


!---------------------------------------------------
!ex9.16b 梁上移动弹簧-质量的瞬态分析-生死单元方法
finish
/clear
/config,nres,2000
/prep7
lb=10   !梁长度
ne=50   !梁划分的单元数
nn=ne+1 !节点数
p=10000  !荷载值
v=36*1e3/3600 !移动速度36km/h
deltl=lb/ne
deltt=deltl/v  !移动一个单元所需时间
em=2.1e11
area=0.18
im=0.0054
deng=7800
f1=acos(-1)/2/lb/lb*sqrt(em*im/(area*deng))

!定义质量单元
et,1,mass21,,,4
r,1,P/9.8
!定义弹簧参数
kstif=1e7
et,2,14,,2
r,2,kstif
!定义梁单元
et,3,beam3
mp,ex,3,em
mp,nuxy,3,0.2
mp,dens,3,deng
r,3,area,im,1.0
!生成质量单元
type,1
real,1
*do,i,1,nn
n,i,(i-1)*deltl,0.5
E,I
*enddo

*do,i,1,nn
n,NN+i,(i-1)*deltl,
*enddo
!生产弹簧单元
type,2  
real,2
*do,i,1,nn
en,100+i,i,nn+i
*enddo
!生成梁单元
type,3
real,3
MAT,3
*do,i,1,ne
e,NN+i,NN+i+1
*enddo

d,NN+1,ux,,,,,uy
d,NN+nn,uy
finish

/solu
antype,trans
nlgeom,on
nropt,full
acel,,9.8
esel,s,real,,1
ekill,all
allsel,all
timint,off
time,1e-5
nsubst,2
kbc,1
solve

timint,on
outres,all,all
deltim,deltt/10
kbc,1
autots,on

ealive,1
time,deltt
solve


*do,i,2,nn-1
ekill,i-1
ealive,i
time,i*deltt
solve
*enddo

ekill,i
time,nn*deltt
solve

esel,s,real,,1
ekill,all
allsel,all
time,lb/v+5/f1
solve

finish
/post26
nc=node(lb/2,0,0)
nsol,2,nc,u,y
plvar,2

