!ex8.23 两片索网找形与分析
finish
/clear
/filname,ex823
/prep7
netnx=40
netny=30
netsiz=2.2
f=10.0
a=0.00222
t0=1e5
istran=0.999
et,1,link10
r,1,a,istran
mp,ex,1,t0/(istran*a)
mp,prxy,1,0.3

xa=netnx*netsiz/2
xb=netny*netsiz/2
*do,i,1,netnx-1
xi=i*netsiz-xa
yi=sqrt(1-xi*xi/xa/xa)*xb
k,2*i-1,xi,-yi
k,2*i,xi,yi
l,2*i-1,2*i
*enddo
*get,kpmax,kp,,count
*do,i,1,netny-1
yi=i*netsiz-xb
xi=sqrt(1-yi*yi/xb/xb)*xa
k,kpmax+2*i-1,-xi,yi
k,kpmax+2*i,xi,yi
l,kpmax+2*i-1,kpmax+2*i
*enddo
nummrg,all
cm,kpcm,kp
lovlap,all
cmsel,s,kpcm
dk,all,all
allsel,all
!提升约束,抛物线形式
!y=-4*f/l/l*x*x+f
l0=netnx*netsiz
*do,i,1,netnx/2
xi=(i-1)*netsiz
dy=-4*f*xi*xi/l0/l0+f
dk,kp(xi,0,0),ux,,,,uy
dk,kp(xi,0,0),uz,dy
dk,kp(-xi,0,0),ux,,,,uy
dk,kp(-xi,0,0),uz,dy
*enddo
lesize,all,,,1
latt,1,1,1
lmesh,all
/solu
antype,0
nlgeom,on
sstif,on
nsubst,10
solve
finish
/prep7
upgeom,1,last,last,ex823,rst
*do,i,1,netnx/2
xi=(i-1)*netsiz
dk,kp(xi,0,0),uz
dk,kp(-xi,0,0),uz
*enddo
*do,i,1,5
finish
/solu
solve
finish
/prep7
upgeom,1,last,last,ex823,rst
*enddo
!索内力在99.3kN-102.1kN之间
mp,ex,1,1.9e11
r,1,a,t0/(1.9e11*a)
/solu
solve
finish
!索力几乎不变



