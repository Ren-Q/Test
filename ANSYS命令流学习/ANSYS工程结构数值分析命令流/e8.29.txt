!-------------------------------
!EX8.29 预应力简支梁弹性分析
!节点耦合法，初应变施加预应力
!-------------------------------
finish
/clear
/prep7
!0.定义变量
emst=1.95e5
as=139
tf=180e3
denss=7921e-12
emcon=3.45e4
densc=2300e-12
r0=9000
b=150
h=200
d0=40
dd=200-2*d0
spanl=3000
!经计算推导可求得两切线交点距离梁底的距离
d1=(39-3*sqrt(29))/35*dd-d0
!1.定义单元与材料性质--------------------
et,1,solid95
et,2,link8
mp,ex,1,emcon
mp,prxy,1,0.2
mp,dens,1,densc
mp,ex,2,emst
mp,prxy,2,0.3
mp,dens,2,denss
r,1
r,2,as,tf/(emst*as)*1.01225
!2.分别创建体和力筋线
blc4,,,b,h,spanl
lsel,none
k,9,b/2,h-d0
k,10,b/2,-d1,spanl/2
k,11,b/2,h-d0,spanl
l,9,10
l,10,11
lfillt,13,14,r0
lcomb,all
wpoff,,,spanl/2
lsbw,all
cm,sline,line
lsel,all
vsbw,all
wpcsys,-1
!3.分别划分单元网格
cmsel,s,sline
latt,2,2,2
lesize,all,40
lmesh,all
esize,40
vatt,1,1,1
lsel,s,length,,h
lesize,all,,,10
lsel,all
mshape,0
mshkey,1
vmesh,all
!4.定义节点耦合自由度
cmsel,s,sline
nsll,s,1
cm,gsnode,node
*get,senum,node,,count
*dim,snode,,senum
*dim,cnode,,senum

*get,nodei,node,,num,min
snode(1)=nodei
*do,i,2,senum
snode(i)=ndnext(snode(i-1))
*enddo

allsel,all
cmsel,u,gsnode
*do,i,1,senum
nodei=snode(i)
nodej=nnear(nodei)
cnode(i)=nodej
nsel,u,,,nodej
*enddo

allsel,all
*do,i,1,senum
cp,next,all,snode(i),cnode(i)
*enddo

!5.定义约束
lsel,s,loc,z,0
lsel,a,loc,z,spanl
lsel,r,loc,y,0
dl,all,,uy
dk,kp(0,0,0),ux,,,,uz
dk,kp(0,0,spanl),ux
allsel,all
finish
!6.施加荷载并求解
/solu
acel,,9800
solve
finish
!7.查看结果
/post1
pldisp,1 !结果为2.147
etable,sigs,ls,1
plls,sigs,sigs,1 
pretab,sigs !大部分结果为1295MPa=180000/139

path,kzhx,2
ppath,1,,0,0,spanl/2
ppath,2,,b,0,spanl/2
pdef,sigc,s,z
plpath,sigc !结果-15.473～-15.525MPa

path,kzsx,2
ppath,1,,b/2,0,spanl/2
ppath,2,,b/2,h,spanl/2
pdef,sigc,s,z
plpath,sigc !结果3.954～-15.525MPa
!按弹性理论的计算结果为3.93～15.67MPa



