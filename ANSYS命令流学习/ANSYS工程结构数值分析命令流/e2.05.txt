!EX2.5腹板式渐开线直齿圆柱齿轮建模
FINISH
/CLEAR
/PREP7
!1.定义齿轮参数
M=5
Z=55
REFA=20
HAX=1
CX=0.25
ROUF=0.38*M
KS=6
!1.1以下多为计算值，也可调正，但注意相互关系
HA=HAX*M
HF=(HAX+CX)*M
PI=ACOS(-1)
D=M*Z
DB=D*COS(REFA*PI/180)
DA=D+2*HA
DF=D-2*HF
ALFAD=ACOS(DB/DA)
*IF,DB,GT,DF,THEN
ALFAG=0.0
*ELSE
ALFAG=ACOS(DB/DF)
*ENDIF
B=0.22*D
!1.2 轴孔参数设定
DTA=6*M
DAX=D/5
D2=1.6*DAX
D1=0.25*(DA-2*DTA-D2)
D0=0.5*(DA-2*DTA+D2)
C=0.3*B
NJ=0.5*M
NJ1=0.5*M
R=5
S=1.5*DAX
!2.创建齿廓面
!2.1用极坐标方程计算渐开线上的点，取20个点拟合该渐开线
CSYS,1
N=20
*DO,I,1,N
ALFAI=ALFAG+((ALFAD-ALFAG)/(N-1))*(I-1)
ROUI=0.5*DB/COS(ALFAI)
CTAI=TAN(ALFAI)-ALFAI
K,I,ROUI,CTAI*180/PI
*ENDDO
!2.2 利用上述关键点创建线，并合并之
*DO,I,1,N-1
L,I,I+1
*ENDDO
CTAI=(TAN(REFA*PI/180)-REFA*PI/180)*180/PI
CTAI=CTAI+360/(4*Z)
LGEN,,ALL,,,,-CTAI,,,,1
CSYS,0
*IF,DB,GT,DF,THEN
K,N+1,KX(1)-(DB-DF)/2,KY(1)
L,1,N+1
*ENDIF
LCOMB,ALL
NUMCMP,ALL
!2.3做对称操作
LSYMM,Y,ALL
!2.4 在齿根圆上创建单齿部分的两个关键点，并倒角
CSYS,1
K,5,0.5*DF,360/(2*Z)
K,6,0.5*DF,-360/(2*Z)
KP1=KNEAR(6)
L,6,KP1
KP1=KNEAR(5)
L,5,KP1
LFILLT,1,3,ROUF
LFILLT,2,4,ROUF
KSEL,S,LOC,X,DA/2
*GET,KP1,KP,0,NUM,MIN
KP2=KPNEXT(KP1)
L,KP1,KP2
ALLSEL
NUMCMP,ALL
!2.5 复制单齿齿廓线Z个，利用线创建面；建议一另外圆面相减，形成齿廓面
LGEN,Z,ALL,,,,360/Z
NUMMRG,KP
AL,ALL
CYL4,,,DA/2-0.5*(DTA+HA+HF)
ASBA,1,2
NUMCMP,ALL
CSYS,0
AGEN,1,1,,,,,B/2,,,1
!3.创建腹板及轴孔的截面
!3.1 创建腹板及轴孔截面的一半线
*GET,KP0,KP,0,NUM,MAX
K,KP0+1,0,DAX/2
K,KP0+2,0,DAX/2,S/2-NJ1
K,KP0+3,0,DAX/2+NJ1,S/2
K,KP0+4,0,D2/2-NJ,S/2
K,KP0+5,0,D2/2,S/2-NJ
K,KP0+6,0,D2/2,C/2
K,KP0+7,0,DA/2-DTA,C/2
K,KP0+8,0,DA/2-DTA,B/2-NJ
K,KP0+9,0,DA/2-DTA+NJ,B/2
K,KP0+10,0,DA/2-0.5*(DTA+HA+HF),B/2
K,KP0+11,0,DA/2-0.5*(DTA+HA+HF)
*DO,I,1,10
L,KP0+I,KP0+I+1
*ENDDO
!3.2 对上述线进行倒角
LSEL,S,LOC,X,0
LSEL,R,LOC,Y,D2/2,DA/2-DTA-0.01
*GET,L1,LINE,0,NUM,MIN
L2=LSNEXT(L1)
LFILLT,L1,L2,R
LSEL,S,LOC,X,0
LSEL,R,LOC,Y,D0/2-D1/2,DA/2-DTA 
*GET,L1,LINE,0,NUM,MIN
L2=LSNEXT(L1)
LFILLT,L1,L2,R
!3.3 创建腹板及轴孔截面
LSEL,S,LOC,X,0
LSEL,R,LOC,Y,DAX/2,DA/2-0.5*(DTA+HA+HF)
LSYMM,Z,ALL
NUMMRG,KP
NUMCMP,ALL
AL,ALL
!3.4 旋转面创建体
*GET,KP0,KP,0,NUM,MAX
K,KP0+100,,,B/2
K,KP0+101,,,-B/2
VROTAT,2,,,,,,KP0+100,KP0+101,,KS
!3.5 拖拉齿廓面创建体
L,KP0+100,KP0+101
*GET,L1,LINE,0,NUM,MAX
VDRAG,1,,,,,,L1
ALLSEL
NUMMRG,ALL
NUMCMP,ALL
!4. 创建圆孔，方法同V带轮
CM,V1CM,VOLU
*AFUN,DEG
WPOFF,,,-B
X0=D0/2*SIN(180/KS)
Y0=D0/2*COS(180/KS)
CYL4,X0,Y0,D1/2,,,,2*B
*GET,V1,VOLU,,NUM,MAX
WPCSYS
CSYS,1
VGEN,KS,V1,,,,360/KS
CMSEL,U,V1CM
CM,V2CM,VOLU
ALLSEL
VSBV,V1CM,V2CM
!5.整理和视图
/VIEW,1,1,1,1
VPLOT
