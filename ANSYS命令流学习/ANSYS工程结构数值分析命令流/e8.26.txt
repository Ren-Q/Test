!------------------------------------------------------
!EX8.26 钢筋混凝土简支梁数值分析
!分离式模型,关闭压碎,keyopt(1)=0,keyopt(7)=1
!力加载,位移收敛准则，误差1.5%,1/4模型分析
!--------------------------------------------
finish
/clear
/config,nres,2000
/prep7
!1.定义单元与材料性质--------------------
et,1,solid65,,,,,,,1   !K1=0,k7=1
et,2,link8
mp,ex,1,13585
mp,prxy,1,0.2
fc=14.3
ft=1.43
tb,concr,1
tbdata,,0.5,0.95,ft,-1
tb,miso,1,,11
tbpt,,0.0002,fc*0.19
tbpt,,0.0004,fc*0.36
tbpt,,0.0006,fc*0.51
tbpt,,0.0008,fc*0.64
tbpt,,0.001,fc*0.75
tbpt,,0.0012,fc*0.84
tbpt,,0.0014,fc*0.91
tbpt,,0.0016,fc*0.96
tbpt,,0.0018,fc*0.99
tbpt,,0.002,fc
tbpt,,0.0033,fc*0.85
mp,ex,2,2.0e5
mp,prxy,2,0.3
tb,biso,2
tbdata,,300,0
pi=acos(-1)
r,1,0.25*pi*22*22
r,2,0.25*pi*22*22/2
r,3,0.25*pi*10*10
r,4,0.25*pi*10*10/2
!2.创建几何模型
blc4,,,150/2,300,2000/2
*do,i,1,9
wpoff,,,100
vsbw,all
*enddo
wpcsys,-1
wpoff,,,50
vsbw,all
wpcsys,-1
wprota,,-90
wpoff,,,30
vsbw,all
wpoff,,,240
vsbw,all
wpcsys,-1
wpoff,30
wprota,,,90
vsbw,all
wpcsys,-1
!3.划分钢筋网格
elemsiz=50
lsel,s,loc,x,30
lsel,r,loc,y,30
cm,zj,line
latt,2,1,2
lesize,all,elemsiz
lsel,s,loc,x,75
lsel,r,loc,y,30
cm,zjb,line
latt,2,2,2
lesize,all,elemsiz
lsel,s,loc,x,30
lsel,r,loc,y,270
cm,jlj,line
latt,2,3,2
lesize,all,elemsiz
lsel,s,tan1,z
lsel,r,loc,y,30,270
lsel,r,loc,x,30,70
lsel,u,loc,z,50
cm,gj,line
latt,2,3,2
lesize,all,elemsiz
lsel,s,loc,z,0
lsel,r,loc,y,30,270
lsel,r,loc,x,30,70
cm,gjb,line
latt,2,4,2
lesize,all,elemsiz
lsel,all
cmsel,s,zj
cmsel,a,zjb
cmsel,a,jlj
cmsel,a,gj
cmsel,a,gjb
cm,gj,line
lmesh,all
lsel,all
!4.划分混凝土网格
vatt,1,,1
mshkey,1
esize,elemsiz
vmesh,all
allsel,all
!5.施加荷载和约束
lsel,s,loc,y,0
lsel,r,loc,z,900
dl,all,,uy
asel,s,loc,z,0
da,all,symm
asel,s,loc,x,75
da,all,symm
!-----------------
p0=180000
q0=p0/150/100
asel,s,loc,z,0,50
asel,r,loc,y,300
sfa,all,1,pres,q0
allsel,all
!6.求解控制设置
/solu
antype,0
nsubst,80
outres,all,all
autos,on
neqit,50
cnvtol,u,,0.015
solve

!7.进入POST1查看结果
/post1
set,last
pldisp,1

esel,s,type,,2
etable,saxl,ls,1
plls,saxl,saxl

esel,s,type,,1
/device,vector,on
plcrack

eall
!8.进入时程后处理
/post26
nsol,2,205,u,y
prod,3,1,,,,,,p0/1000
prod,4,2,,,,,,-1
xvar,4
plvar,3




