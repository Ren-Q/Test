!----------------------------------
!EX6.10 提取结构刚度矩阵及处理
finish
/clear
/filname,hbfile
/prep7
et,1,beam3
mp,ex,1,2e5
r,1,1e-2,32e-5,0.5
n,1
n,2,0,4
n,3,4,4
n,4,4,0
en,1,2,3
en,2,1,2
en,3,4,3
f,2,fx,5
sfbeam,1,1,pres,10,,,,2,-1
sfbeam,2,1,pres,3
d,1,all
d,4,all
/solu
solve
finish
/aux2
file,hbfile,full
hbmat,hbfile,txt,,ascii,stiff,yes
finish
!----------------------------------------
!从hbfile.txt读入数据，并还原为满矩阵存储
*dim,contline,,5
*vread,contline(1),hbfile,txt,,,5,,,1
(5f14.0)
totcrd=contline(1)
ptrcrd=contline(2)
indcrd=contline(3)
valcrd=contline(4)
rhscrd=contline(5)
*vread,contline(1),hbfile,txt,,,4,,,2
(a3,11x,4f14.0)
nrow=contline(2)
ncol=contline(3)
strline=
contline=
*if,rhscrd,eq,0,then
ls0=4
*else
ls0=5
*endif
*dim,pointr,,ptrcrd
*dim,rowind,,indcrd
*dim,values,,valcrd
*dim,rhsval,,rhscrd
*vread,pointr(1),hbfile,txt,,,ptrcrd,,,ls0
(f14.0)
*vread,rowind(1),hbfile,txt,,,indcrd,,,ls0+ptrcrd
(f14.0)
*vread,values(1),hbfile,txt,,,valcrd,,,ls0+ptrcrd+indcrd
(d25.15)
*vread,rhsval(1),hbfile,txt,,,rhscrd,,,ls0+ptrcrd+indcrd+valcrd
(d25.15)
*dim,smatr,,nrow,ncol
*do,icol,1,ncol
stacol=pointr(icol)
endcol=pointr(icol+1)
*do,irow,stacol,endcol-1
truerow=rowind(irow)
smatr(truerow,icol)=values(irow)
*enddo
*enddo
*do,irow,1,nrow
*do,icol,1,ncol
smatr(irow,icol)=smatr(icol,irow)
*enddo
*enddo
pointr=
rowind=
values=
rhsval=
icol=
irow=
ls0=
stacol=
endcol=
truerow=
totcrd=
ptrcrd=
indcrd=
valcrd=
rhscrd=