!ex9.18a 单点响应谱典型分析过程
finish
/clear
!1.建模
!加速度谱计算
tg=0.45
refmax=0.08
gra=9.8
kes=0.02
eta1=0.02+(0.05-kes)/8
eta2=1+(0.05-kes)/(0.06+1.7*kes)
gama=0.9+(0.05-kes)/(0.5+5*kes)
*dim,ttt,,14
*dim,fre,,14
*dim,ace,,14

ttt(1)=0,0.1,0.45,0.5,0.6,0.8,1.0,1.5,2.0,2.25,3.0,4.0,5.0,6.0
ace(1)=0.45*refmax*gra
ace(2)=eta2*refmax*gra
ace(3)=ace(2)
*do,i,4,10
ace(i)=(tg/ttt(i))**gama*eta2*refmax*gra
*enddo
*do,i,11,14
tempi=eta2*0.2**gama-eta1*(ttt(i)-5*tg)
ace(i)=tempi*refmax*gra
*enddo
fre(14)=1e5
*do,i,2,14
fre(15-i)=1/ttt(i)
*enddo
*do,i,1,14
ttt(15-i)=ace(i)
*enddo
*do,i,1,14
ace(i)=ttt(i)
*enddo


/prep7
et,1,beam3
mp,ex,1,3e10
mp,prxy,1,0.2
mp,dens,1,2500
r,1,0.6,0.00045,0.3
k,1
k,2,,4
k,3,4,4
k,4,4
l,1,2
l,2,3
l,3,4
esize,0.5
lmesh,all
dk,1,all
dk,4,all
finish
!2.获得模态解
/solu
antype,modal
modopt,lanb,10
solve
finish
!3.获得谱解
/solu
antype,spectr
spopt,sprs,10,yes
dmprat,0.02
sed,1
svtyp,2
*do,i,1,14
freq,fre(i)
sv,,ace(i)
*enddo

solve
finish
!4.扩展模态
/solu
antype,modal
expass,on
mxpand,10,,,yes,0.005
solve
finish
!5.模态合并
/solu
antype,spectr
srss,0.15,disp
solve
finish
!6.观察结果
/solu
/post1
set,list
/input,,mcom
pldisp,1
prnsol,dof
presol,elem
prrsol,f

set,1,1
etable,m1,smisc,6
etable,m2,smisc,12
plls,m1,m2

!===================
!ex9.18b 单点响应谱简化分析过程
finish
/clear
!1.建模与加速度谱计算同上
/prep7

!2.获得模态解
/solu
antype,modal
modopt,lanb,10
mxpand,10,,,yes,0.005
sed,1
svtyp,2
*do,i,1,14
freq,fre(i)
sv,,ace(i)
*enddo
srss,0.15,disp
solve
finish
!3.观察结果同上

