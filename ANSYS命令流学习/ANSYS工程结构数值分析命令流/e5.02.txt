!EX5.2 BEAM18x系列后处理示例
finish 
/clear 
/prep7
k,1
k,2,5
k,3,5,5
l,1,2
et,1,beam189
keyopt,1,4,2
mp,ex,1,2e11 
mp,prxy,1,0.3
sectype,1,beam,rect
secdata,0.24,0.3,2,2
latt,1,,1,,3,,1 
eno=20
lesize,all,,,eno
lmesh,all 
dk,1,all
fk,2,fy,-100000.0
secplot,1,1
slist,1,1,,1,all
finish
/solu
solve 
/post1 
!---------------------------------------------------
/eshape,1 
plesol,s,eqv
/pnum,sval,1
plnsol,s,x
prssol,s,comp
prssol,,prin
*dim,v1,,eno
*dim,v2,,eno
*do,i,1,eno
*get,v1(i),secr,i,s,x,max
*get,v2(i),secr,i,s,x,min
*enddo
!----------------------------------------------------
!得到各单元各节点截面各角栅点的应力分量Sxx,Sxz,Sxy
csnnum=81
*dim,mysss,,eno,csnnum
*do,i,1,eno
*do,k,1,csnnum
etable,var%k%,ls,k
*enddo
*do,j,1,csnnum
*get,mysss(i,j),elem,i,etab,var%j%
*enddo
*enddo
!--------------------------------------------------------
!输出到文件（必须用读入文件方式执行，即/input）
*cfopen,myres,txt
*vwrite,
('**********单元最大应力和最小应力---Sxx*********')
*vwrite
('单元号        最大应力          最小应力')
*do,i,1,eno
a1=v1(i)
a2=v2(i)
*vwrite,i,a1,a2
(f6.0,2x,e15.6,5x,e15.6)
*enddo
*vwrite
('**********各单元各节点截面各角栅点应力分量***************')
*do,i,1,eno
*vwrite,i
('单元编号=',f6.0)
*vwrite
('顺序号',8x,'应力')
*do,j,1,csnnum/3
a1=mysss(i,3*j-2)
a2=mysss(i,3*j-1)
a3=mysss(i,3*j)
*vwrite,j,a1,a2,a3
(f6.0,8x,e15.6,2x,e15.6,2x,e15.6)
*enddo
*enddo
*cfclose
